apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'org.kordamp.markdown.convert'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'com.moowork.node'
apply plugin: 'project-report' // adds dependencyReport and htmlDependencyReport tasks

sourceCompatibility = 1.8

// Defaults for variables
run.environment("DB_DRIVER", "sqlserver")
run.environment("ANET_DB_SERVER", "localhost")
run.environment("ANET_DB_NAME", "testAnet")
run.environment("ANET_DB_USERNAME", "anetUser")
run.environment("ANET_DB_PASSWORD", "Test-P@ssw0rd")
run.environment("ANET_SA_PASSWORD", "SA-P@ssw0rd")
run.environment("ANET_DB_PORT", "1433")

// If defined, load alternatives for variables
if (file('localSettings.gradle').exists()) {
	apply from: 'localSettings.gradle'
}

// In this section you declare where to find the dependencies of your project
repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
	maven { url "https://repo.spring.io/plugins-release" }
}

// In this section you declare the dependencies for your production and test code
dependencies {
	// The production code uses the SLF4J logging API at compile time
	compile group: 'io.dropwizard', name: 'dropwizard-core', version: '1.3.7'
	compile group: 'io.dropwizard', name: 'dropwizard-jdbi', version: '1.3.7'
	compile group: 'io.dropwizard', name: 'dropwizard-migrations', version: '1.3.7'
	compile group: 'io.dropwizard', name: 'dropwizard-auth', version: '1.3.7'
	compile group: 'io.dropwizard', name: 'dropwizard-views-freemarker', version: '1.3.7'
	compile group: 'io.dropwizard', name: 'dropwizard-assets', version: '1.3.7'
	compile group: 'org.antlr', name: 'stringtemplate', version: '3.2.1' // optional dependency of jdbi

	compile 'org.xerial:sqlite-jdbc:3.25.2' //If using SQLite
	compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4' // If using postgresql
	compile group: 'org.liquibase', name: 'liquibase-core', version: '3.6.2' // or newer, for postgresql dropPrimaryKey fix [CORE-3129]

	compile 'javax.mail:mail:1.4.7'
	compile 'ch.qos.logback:logback-classic:1.2.3'
	compile 'ch.qos.logback:logback-core:1.2.3'
	compile 'ch.qos.logback:logback-access:1.2.3'
	compile 'com.github.waffle:waffle-jna:1.9.1'
	compile 'com.graphql-java:graphql-java:10.0'
	compile 'com.graphql-java:java-dataloader:2.2.0'
	compile 'io.leangen.graphql:spqr:0.9.8'
	compile 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20180219.1'

	// Used for converting GraphQL request output to XML:
	compile 'com.github.javadev:underscore-lodash:1.26'
	// For JSON schema validation
	compile 'com.github.everit-org.json-schema:org.everit.json.schema:1.9.2'

	compile "com.microsoft.sqlserver:mssql-jdbc:6.2.2.jre8"

	// used for writing Excel documents
	compile group: 'org.apache.poi', name: 'poi', version: '4.0.0'
	compile group: 'org.apache.poi', name: 'poi-ooxml', version: '4.0.0'

	testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: '1.3.7'
	testCompile group: 'io.dropwizard', name: 'dropwizard-client', version: '1.3.7'
	testCompile 'org.apache.commons:commons-io:1.3.2'
}

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    reports {
        xml.enabled true
		html.enabled false
	}
}

//Set project version to git tag
def proc = "git describe".execute(null, projectDir);
proc.waitFor();
project.version = proc.text.trim();

def projectBranch = "git rev-parse --abbrev-ref HEAD".execute(null, projectDir);
projectBranch.waitFor();
projectBranch = projectBranch.text.trim();

project.group = "ncia"

mainClassName = "mil.dds.anet.AnetApplication"
String anetConfig = "anet.yml"

applicationDefaultJvmArgs = ["-Djava.library.path=MY_APP_HOME/lib"]

// Set up DB parameters, with sensible defaults:
String dbDriver = run.environment['DB_DRIVER'] ?: 'sqlite'
String dbLoc = run.environment['ANET_DB_SERVER'] ?: 'localhost'
String dbName = run.environment['ANET_DB_NAME'] ?: 'anet'
if (run.environment['ANET_DB_PORT']) {
	dbLoc = dbLoc + ":" + run.environment['ANET_DB_PORT'].toString()
}
if (!run.environment['ANET_DB_USERNAME']) {
	run.environment('ANET_DB_USERNAME', run.environment['USER'])
}

if (dbDriver == "sqlserver") {
	run.environment("ANET_DB_DRIVER", "com.microsoft.sqlserver.jdbc.SQLServerDriver")
	run.environment("ANET_DB_URL", "jdbc:sqlserver://${dbLoc};databaseName=${dbName}")
} else if (dbDriver == "postgresql") {
	run.environment("ANET_DB_DRIVER", "org.postgresql.Driver")
	run.environment("ANET_DB_URL", "jdbc:postgresql://${dbLoc}/${dbName}")
} else if (dbDriver == "sqlite") {
	run.environment("ANET_DB_DRIVER", "org.sqlite.JDBC")
	run.environment("ANET_DB_URL", "jdbc:sqlite:development.db?foreign_keys=on")
	run.environment("ANET_DB_DATE_STRING_FORMAT", "yyyy-MM-dd hh:mm:ss.SSS Z")
	run.environment("ANET_DB_DATE_CLASS", "text")
} else {
	throw new GradleException( "Unsupported DB_DRIVER value \'${dbDriver}\'")
}

test.environment = run.environment

run.args = ["server", anetConfig]

task checkConfig(dependsOn: 'compileJava', type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	environment(run.environment)
	main = mainClassName
	args = ["check", anetConfig]
}

task buildClient(dependsOn: 'yarn', type: YarnTask) {
    inputs.file("client/yarn.lock").withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.dir("client/src").withPathSensitivity(PathSensitivity.RELATIVE)
	inputs.dir("client/config").withPathSensitivity(PathSensitivity.RELATIVE)
	inputs.dir("client/public").withPathSensitivity(PathSensitivity.RELATIVE)
	outputs.dir("$buildDir/resources/main/assets/client")
	outputs.cacheIf { true }
	args = ['run', 'build']
}

task dbInit(dependsOn: 'compileJava', type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	environment(run.environment)
	main = mainClassName
	args = ["init", anetConfig]
	standardInput = System.in
}

task dbStatus(dependsOn: 'compileJava', type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	environment(run.environment)
	main = mainClassName
	args = ["db", "status", anetConfig]
}

task dbMigrate(dependsOn: 'compileJava', type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	environment(run.environment)
	main = mainClassName
	args = ["db", "migrate", anetConfig]
	if (project.hasProperty("dry-run")) {
		args += "--dry-run"
	}
}

task dbTest(dependsOn: 'compileJava', type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	environment(run.environment)
	main = mainClassName
	args = ["db", "test", anetConfig]
}

task dbRollback(dependsOn: 'compileJava', type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	environment(run.environment)
	main = mainClassName
	args = ["db", "rollback", anetConfig, "--count", "1"]
	if (project.hasProperty("dry-run")) {
		args += "--dry-run"
	}
}

task dbDrop(dependsOn: 'compileJava', type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	environment(run.environment)
	main = mainClassName
	args = ["db", "drop-all", "--confirm-delete-everything", anetConfig]
}

task dockerBuildImage(dependsOn: installDist, type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
	description = "Builds anet-app-server container image"
	doFirst {
		copy {
			from 'docker/anet-app-server/Dockerfile'
			into 'build/install'
		}
	}
	inputDir =  project.file('build/install')
	def resultTags = ['ncia/anet-app-server:'+project.version]
	if (projectBranch=="candidate")
		resultTags << 'ncia/anet-app-server:'+projectBranch
	if (projectBranch=="master")
		resultTags << 'ncia/anet-app-server:latest'
	tags = resultTags
}

task dockerPushCandidateImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
	imageName = 'ncia/anet-app-server'
	tag = 'candidate'
}

task dockerPushLatestImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
	imageName = 'ncia/anet-app-server'
	tag = 'latest'
}

task dockerPushImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
	imageName = 'ncia/anet-app-server'
	tag = project.version
}

task dockerPullDB(type: com.bmuschko.gradle.docker.tasks.image.DockerPullImage) {
	repository = "ncia/anet-mssql-linux"
	description = "Pulls a docker image for the ANET DB from " + repository
	tag = "latest"
}

task dockerCreateDB(dependsOn: dockerPullDB, type: com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer) {
	imageId = dockerPullDB.getImageId()
	containerName = "anet-mssql-server"
	description = "Creates an ANET MS SQL DB container named " + containerName
	binds = ["${projectDir}":"/hostdata"] + run.environment.get("DOCKER_MOUNTS", [:])
	withEnvVar('ACCEPT_EULA', 'Y')
	withEnvVar('SA_PASSWORD', run.environment["ANET_SA_PASSWORD"])
	withEnvVar('DB_NAME', run.environment["ANET_DB_NAME"])
	withEnvVar('DB_USER', run.environment["ANET_DB_USERNAME"])
	withEnvVar('DB_USER_PASSWORD', run.environment["ANET_DB_PASSWORD"])
	portBindings = ["1433:"+run.environment["ANET_DB_PORT"]]
}

// need to make this invoke dbCreateContainer if there is no anet-mssql-server and possibly get newer image if available
task dockerStartDB(type: com.bmuschko.gradle.docker.tasks.container.DockerStartContainer) {
	description = "Starts anet-mssql-server container"
	targetContainerId { "anet-mssql-server" }
}

task dockerStopDB(type: com.bmuschko.gradle.docker.tasks.container.DockerStopContainer) {
	description = "Stops anet-mssql-server container"
	targetContainerId { "anet-mssql-server" }
}

task dockerRemoveDB(type: com.bmuschko.gradle.docker.tasks.container.DockerRemoveContainer) {
	description = "Removes anet-mssql-server container"
	targetContainerId { "anet-mssql-server" }
}

task dockerWaitForDB(type: com.bmuschko.gradle.docker.tasks.container.DockerExecContainer) {
	description = "Waiting until the DB server has started"
	targetContainerId { "anet-mssql-server" }
	cmd = ["/opt/waitTillServiceStarted.sh"]
}

task dockerLoadDB(type: com.bmuschko.gradle.docker.tasks.container.DockerExecContainer) {
	description = "Loads default data into anet-mssql-server container"
	targetContainerId { "anet-mssql-server" }
	cmd = ["/opt/mssql-tools/bin/sqlcmd", "-S", run.environment["ANET_DB_SERVER"], "-U", run.environment["ANET_DB_USERNAME"], "-P", run.environment["ANET_DB_PASSWORD"], "-d", run.environment["ANET_DB_NAME"], "-a", "32767", "-i", "/hostdata/insertBaseData.sql"]
}

classes.dependsOn buildClient

//Configure the Java Checkstyle settings. Run with ./gradlew check
checkstyle {
	configFile = rootProject.file('gradle/checkstyle.xml')
	toolVersion = '7.5'
}

//Configure extra files to include the distribution file
distributions {
	main {
		contents {
			into('docs/') {
				from('anet.yml') { rename('anet.yml', 'anet.yml.template') }
				from(markdownToHtml)
			}
			into('lib/') {
				from('lib/sqljdbc_auth.dll')
			}
		}
	}
}

//Add gradle plugin repos
buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.kordamp:markdown-gradle-plugin:1.2.0"
		classpath 'com.bmuschko:gradle-docker-plugin:4.0.1'
		classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
	}
}

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('MY_APP_HOME', '\$APP_HOME')
		windowsScript.text = windowsScript.text.replace('MY_APP_HOME', '%APP_HOME%')
	}
}

//Configure Markdown plugin
allprojects {
	markdownToHtml.sourceDir = file("docs")
	markdownToHtml.outputDir = file("$buildDir/documentation")
	markdownToHtml.hardwraps = true
}

node {
	nodeModulesDir = file("${project.projectDir}/client")
	version = '8.11.2'
	download = true
}
